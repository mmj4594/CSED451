#version 460

layout(location = 0) in vec3 aPos;
layout(location = 1) in vec3 aColor;
layout(location = 2) in vec3 aNormal;
layout(location = 3) in vec2 aTexCoord;

out vec3 fN;
out vec3 fE;
out vec3 fL_point;
out vec3 fL_directional;
out vec4 fragColor;
out vec2 texCoord;

uniform int lightType;
uniform vec4 ambientProduct;
uniform vec4 diffuseProduct_point;
uniform vec4 diffuseProduct_directional;
uniform vec4 specularProduct_point;
uniform vec4 specularProduct_directional;
uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;
uniform vec4 lightPosition_point;
uniform vec4 lightPosition_directional;
uniform float shininess;
uniform int shaderCode;
uniform bool enableTexture;
uniform int mappingCode;

void main() {
	//Gouraud shading
	if(shaderCode == 0) {
		vec3 N = normalize(mat3(transpose(inverse(model))) * aNormal);
		vec3 E = normalize(aPos);
		vec3 L_point = normalize(lightPosition_point.xyz - (model * vec4(aPos, 1.0)).xyz);
		vec3 L_directional = normalize(lightPosition_directional.xyz);
		vec3 H_point = normalize(L_point + E);
		vec3 H_directional = normalize(L_directional + E);

		vec4 ambient = ambientProduct;

		//Point light
		float distance = length(lightPosition_point.xyz - (model * vec4(aPos, 1.0)).xyz);
		float attenuation = 1.0 / (1.0 + 0.007 * distance + 0.0002 * (distance * distance));
		float Kd_point = max(dot(L_point, N), 0.0);
		vec4 diffuse_point = attenuation * Kd_point * diffuseProduct_point;
		float Ks_point = pow(max(dot(H_point, N), 0.0), shininess);
		vec4 specular_point = attenuation * Ks_point * specularProduct_point;
		if(dot(L_point, N) < 0.0) specular_point = vec4(0.0, 0.0, 0.0, 1.0);

		//Directional light
		float Kd_directional = max(dot(L_directional, N), 0.0);
		vec4 diffuse_directional = Kd_directional * diffuseProduct_directional;
		float Ks_directional = pow(max(dot(H_directional, N), 0.0), shininess);
		vec4 specular_directional = Ks_directional * specularProduct_directional;
		if(dot(L_directional, N) < 0.0) specular_directional = vec4(0.0, 0.0, 0.0, 1.0);

		if(lightType == 0)
			fragColor = vec4(aColor, 1.0) * (ambient + (diffuse_point + diffuse_directional) + (specular_point + specular_directional));
		else if(lightType == 1)
			fragColor = vec4(aColor, 1.0) * (ambient + diffuse_point + specular_point);
		else if(lightType == 2)
			fragColor = vec4(aColor, 1.0) * (ambient + diffuse_directional + specular_directional);
		fragColor.a = 1.0;
	}

	//Phong shading
	else if(shaderCode == 1) {
		fragColor = vec4(aColor, 1.0);
	}

	fN = mat3(transpose(inverse(model))) * aNormal;
	fE = aPos;
	fL_point = lightPosition_point.xyz - (model * vec4(aPos, 1.0)).xyz;
	fL_directional = lightPosition_directional.xyz;
	gl_Position = projection * view * model * vec4(aPos, 1.0);
	texCoord = aTexCoord;
}